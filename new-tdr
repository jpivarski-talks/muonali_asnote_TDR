#!/usr/bin/env perl

#############################################################################
#
# Perl script to build the TDR, cleanup, and so on.
#
# For help, type:
#
#     >  tdr help
#
#  Created:            Lucas Taylor   6 January 2005
#  Latest revision:   George Alverson   9 November 2005
#
#############################################################################

use Getopt::Long qw(:config require_order); # the require order means that for
#  "tdr command -switch", -switch is passed in ARGV
use File::Basename;   # access to directory, filename and extension of script
use File::Copy;
use File::Spec::Functions; # catfile
use Cwd;              # like Unix pwd but more portable
use Cwd 'abs_path';
use File::Glob 'bsd_glob';

#############################################################################
# Main:
#############################################################################

my $verbose = '';
my $help = '';
my $electronic = '1';
my $backref = '';
my $style = 'tdr';

GetOptions ('verbose!'    => \$verbose,    # negatable: --noverbose, turn on TeX screen output
            'help|?'      => \$help,       # echo basic operations and options
            'electronic!' => \$electronic, # electronic version: ie, coloured links
            'backref!'    => \$backref,    # backrefs in pdf
            'style=s'     => \$style,      # tdr/note/an/cr/in
            );

# set options from passed switches
my $pdfoptions='';
if ($electronic)
{
  $pdfoptions = "\\PassOptionsToClass{electronic";
}
else
{
  $pdfoptions = "\\PassOptionsToClass{paper";
}
if ($backref)
{
  $pdfoptions = "$pdfoptions,backref,$style}{new-cms-tdr}";
}
else
{
  $pdfoptions = "$pdfoptions,$style}{new-cms-tdr}";
}

my $n_args = @ARGV;      # number of elements in argument list, @ARGV


if ($help || substr($ARGV[0],0,1) eq 'h')
{
    &print_usage();
    exit;
}


if ($n_args == 0)
{    print "\n***** No arguments specified *****\n\n";
     die "For more details type>   tdr help\n\n";
}
elsif ($n_args > 3)
{
    print "\n***** Too many arguments specified *****\n\n";
     die "For more details type>   tdr help\n\n";
}

&init();

$arg1 = $ARGV[0];   # array @ARGV contains input arguments
$arg2 = $ARGV[1];
$arg3 = $ARGV[2];

if    ($arg1 eq "build"     || $arg1 eq "b")  { &build       ( $arg2 );}
elsif ($arg1 eq "clean"     || $arg1 eq "c")  { &clean       (       );}
elsif ($arg1 eq "runtime"   || $arg1 eq "r")  { &runtime     ( $arg2 );}
elsif ($arg1 eq "test")                       { &test        ( $arg2 );}
elsif ($arg1 eq "veryclean" || $arg1 eq "v")  { &veryclean   ();}
else{ # Whatever arguments were typed, they don't make sense...
       print "\n    ***** Invalid arguments ***** \n\n";
       die "For more details type>   tdr --help\n\n";
    }





#############################################################################
# Subroutines:
#############################################################################


#############################################################################
sub print_usage {
    print "******************************************************************************\n";
    print "* Builds TDR documents                      Lucas Taylor, 6 January 2005     *\n";
    print "*                                                                            *\n";
    print "* Initialisation (once per session/new shell)                                *\n";
    print "* --------------                                                             *\n";
    print "* > eval `tdr runtime -sh`    // for Bourne-shell or Korn shell              *\n";
    print "* > eval `tdr runtime -csh`   // for c-shell or tc shell                     *\n";
    print "*                                                                            *\n";
    print "* Usage:                                                                     *\n";
    print "* -----                                                                      *\n";
    print "* > tdr help           // display this help                                  *\n";
    print "*                                                                            *\n";
    print "* > tdr [options] build  [target] // process target.tex to produce ps, pdf,..*\n";
    print "*     target=notes_for_authors  // (default): builds documentation           *\n";
    print "*            ctdr      // Computing TDR                                      *\n";
    print "*            ptdr1     // Physics TDR Vol I                                  *\n";
    print "*            ptdr2     // Physics TDR Vol II                                 *\n";
    print "*            ptdr2     // Physics TDR Vol II                                 *\n";
    print "*            dloi      // Diffractive Letter of Intent                       *\n";
    print "*            foo       // builds arbitrary file foo.tex in current directory.*\n";
    print "*       options:                                                             *\n";
    print "*      --verbose: log messages from TeX (default is --noverbose)             *\n";
    print "*      --paper:   produce output for paper output (default is --nopaper)     *\n";
    print "*      --backref: include back references in the bibliography to             *\n";
    print "*                 the pages producing the citations (for reference debug)    *\n";
    print "*                 (default is --nobackref)                                   *\n";
    print "*      --style=:  document style, one of tdr/note/an/cr/in                   *\n";
    print "*                 (default --style=tdr)                                      *\n";
    print "*                                                                            *\n";
    print "* > tdr clean          // deletes temporary output files                     *\n";
    print "* > tdr veryclean      // normal clean and also emacs backups etc.           *\n";
    print "*                                                                            *\n";
    print "******************************************************************************\n";
}



#############################################################################
sub init {
#
# Define various input and output directories
#
    if ($^O eq "MSWin32") {
      $cur_dir = abs_path(cwd());
      $bin_dir = abs_path(dirname($0));
      $top_dir = $bin_dir;
      $gen_dir = $top_dir."/general";
      $tmp_dir = $top_dir."/tmp";
      }
    else {
      $cur_dir = &full_path(&cwd());                # Current directory
      $bin_dir = &full_path(dirname($0));           # Directory containing this script
      $top_dir = &full_path($bin_dir);              # Top level of checkout area - this is where the script is
      $gen_dir = &full_path($top_dir."/general");   # Dir. of document skeleton/style
      $tmp_dir = &full_path($top_dir."/tmp");       # Dir. of temp output, dvi/PS files, etc.
      }

}




#############################################################################
sub build {
#
#   Build {$target}.tex
#
    &clean();
#
#   Take target in this order: (1) command line, (2) environment (3) hardwired default
#
    my $n_args = @ARGV;      # number of elements in argument list, @ARGV

    my $target = $_[0] || $ENV{TDR_TARGET} || "notes_for_authors";
    substr($target,0,1) ne '-' or die "Options ($_[0]) must now go before the command. Sorry.\n";
    $target =~ s/\.tex$//;


#   Find the target tex file to be built
    my $target_tex = "$target".'.tex';

# Look in some sensible places (yeah, this is not elegant but it's clear and it works)
    if   ( -e "$cur_dir/$target_tex")               { $tex_dir = "$cur_dir";           }
    elsif( -e "$top_dir/general/$target_tex")       { $tex_dir = "$top_dir/general";  }
    elsif( -e "$top_dir/ctdr/tex/$target_tex")      { $tex_dir = "$top_dir/ctdr/tex";  }
    elsif( -e "$top_dir/ptdr1/tex/$target_tex")     { $tex_dir = "$top_dir/ptdr1/tex";  }
    elsif( -e "$top_dir/ptdr2/tex/$target_tex")     { $tex_dir = "$top_dir/ptdr2/tex";  }
    elsif( -e "$top_dir/dloi/tex/$target_tex")      { $tex_dir = "$top_dir/dloi/tex";  }
    else {die "Cannot find target file: $target_tex\n";}

    $full_target_tex =  "$tex_dir/$target_tex";
    print "\n",'Building:  ',"$full_target_tex\n\n";

#  Define PATH of places to find other tex files, figures, bibliography stuff
#  For now, only look in a single tree (means e.g. CTDR pictures are not exposed to PTDR)
#  Revisit this if it looks likely to be needed.

    my $ps = ":"; # path separator symbol: as these paths get passed to the local OS, must abide by the conventions
    if ($^O eq "MSWin32") {$ps = ";";} # use ; for Windows

   $tex_path    = "$tex_dir".$ps."$cur_dir/tex".$ps;
   $bib_path    = "$tex_dir/../bib".$ps."$tex_dir/../../bib".$ps."$tex_dir/../../../bib".$ps;  # N.B. should match "cat XXX/*.bib" below
   $fig_path    = "$tex_dir/../fig".$ps."$tex_dir/../../fig".$ps."$tex_dir/../../../fig".$ps;
   $common_path = "$gen_dir".$ps."$gen_dir/tex_extensions".$ps."$tmp_dir".$ps."$cur_dir".$ps."$cur_dir/..".$ps;

# Define directories to be searched by LaTeX, including one where script was invoked
# Note that it will revert automatically to its original value when script completes

$ENV{TEXINPUTS}    = "$tex_path"."$bib_path"."$fig_path"."$common_path".$ENV{TEXINPUTS};
$ENV{TEXPSHEADERS} = $ENV{TEXINPUTS}; # This is required by \includegraphics when using pdftex

#   Set up temporary area and create input LaTeX file (boilerplate + target file)

    -e $tmp_dir || mkdir ($tmp_dir,0755) || die "Failed to create temporary output directory";

    my $temp_target = "${target}_temp";
    my $temp_stem  = "$tmp_dir/$temp_target";
    my $temp_tex   = "$temp_stem.tex";

    unlink($temp_tex);
    &mycat( "$gen_dir/new-skeleton_start.tex", $temp_tex );
#
#   get the definitions.tex file from the tree you are in
#

    if( -e "$tex_dir/definitions.tex")             { &mycat( "$tex_dir/definitions.tex", $temp_tex) ; }
    if( -e "$tex_dir/../definitions.tex")          { &mycat( "$tex_dir/../definitions.tex", $temp_tex) ; }
    if( -e "$tex_dir/../../definitions.tex")       { &mycat( "$tex_dir/../../definitions.tex", $temp_tex) ; }
    if( -e "$tex_dir/../../../definitions.tex")    { &mycat( "$tex_dir/../../../definitions.tex", $temp_tex) ; }
    if( -e "$tex_dir/../../../../definitions.tex") { &mycat( "$tex_dir/../../../../definitions.tex", $temp_tex) ; }
#
#   This is general definitions file - it uses \providecommand so definitions can
#   be overridden just by defining them in one of the above definitions.tex files
#
    if( -e "$gen_dir/definitions.tex" && ($gen_dir ne $tex_dir) )             { &mycat("$gen_dir/definitions.tex", $temp_tex) ; }

    &mycat( $full_target_tex, $temp_tex);
    &mycat( "$gen_dir/skeleton_end.tex", $temp_tex);

#   Set up temporary bibliography style and DB files

    my $temp_bst   = "$tmp_dir/auto_generated.bst";   # Note: this is referred to explicitly from latex
    my $temp_bib   = "$tmp_dir/auto_generated.bib";   # Note: this is referred to explicitly from latex

    unlink($temp_bst);
    unlink($temp_bib);

    copy("$gen_dir/lucas_unsrt.bst", $temp_bst);

    &mycat(bsd_glob("$tex_dir/*.bib"), bsd_glob("$tex_dir/../bib/*.bib"), bsd_glob("$tex_dir/../../bib/*.bib"), $temp_bib);  # Must agree with $bib_path


#   various output log and error files; double quotes allow spaces in paths
    $temp_texq = qq|"$temp_tex"|;
    $temp_stemq = qq|"$temp_stem"|;
    $temp_pdfout = $temp_stem.'.pdftex_out';
    $temp_pdfoutq = qq|"$temp_pdfout"|;
    $temp_pdferr = $temp_stem.'.err';
    $temp_pdferrq = qq|"$temp_pdferr"|;
    $temp_bibout = $temp_stem.'_bib.out';
    $temp_biboutq = qq|"$temp_bibout"|;
    $temp_biberr = $temp_stem.'_bib.err';
    $temp_biberrq = qq|"$temp_biberr"|;
    $temp_pdf = $temp_stem.'.pdf';
    $temp_pdflog = $temp_stem.'.log';

 #  add options
    my $option_target = $temp_target;
    if ($pdfoptions)
    {
      $option_target = qq|"$pdfoptions \\input{$temp_target}"|;
    }

 #   Now run latex, build table of contents, index, etc. and create dvi/PS/etc. files
    if ($^O eq "MSWin32")
    {
        my $orig_dir = Cwd::abs_path;
        chdir($tmp_dir);
        my $dirstring;
        my $files = $ENV{TEXINPUTS};
        $files =~ s/\//\\/g; # replace forward slashes with back slashes
        foreach $dir (split($ps,$files)) {
            if (-e $dir) {$dirstring .= "-include-directory=".'"'.$dir.'" '};
            }
        if (!$verbose)
        {
          my $cmdstring = "pdflatex  -interaction=batchmode $dirstring  $option_target ";
          print ("First pass TeXing file: $temp_stemq\n");
          system ("$cmdstring 1> NUL 2> NUL");
          print ("BibTeXing file: $temp_stemq\n");
          system ("bibtex  $dirstring $temp_stemq 1> $temp_biboutq 2> $temp_biberrq");
          print ("2nd pass TeXing file: $temp_stemq\n");
          system ("$cmdstring 1> NUL 2> NUL");
          print ("final (3rd pass) TeXing file: $temp_stemq\n");
          system ("$cmdstring 1> $temp_pdfoutq 2> $temp_pdferrq ");
        }
        else
        {
          my $cmdstring = "pdflatex  $dirstring  $option_target ";
          print ("First pass TeXing file: $temp_stemq\n");
          system ("$cmdstring");
          print ("BibTeXing file: $temp_stemq\n");
          system ("bibtex  $dirstring $temp_target");
          print ("2nd pass TeXing file: $temp_stemq\n");
          system ("$cmdstring");
          print ("final (3rd pass) TeXing file: $temp_stemq\n");
          system ("$cmdstring");
        }
        chdir( $orig_dir);
    }
    else
    {
        # check for certified CMS version of tex
        my $TeXversion;
        my $baseTeXversion = "3.141592-1.21a-2.2";
        if ( `pdftex -version` =~ /pdfeTeX\s*(\S*)/ ) {$TeXversion=$1};
        if ( -e '/afs/cern.ch/cms/external/tex/bin/i686-pc-linux-gnu/pdftex' )
        {
          if ( `/afs/cern.ch/cms/external/tex/bin/i686-pc-linux-gnu/pdftex -version` =~ /pdfeTeX\s*(\S*)/ ) {$baseTeXversion=$1};
          if ($TeXversion != $baseTeXversion)
          {
            $ENV{'PATH'} = '/afs/cern.ch/cms/external/tex/bin/i686-pc-linux-gnu:'.$ENV{'PATH'};
          }
        }
        elsif ($TeXversion != $baseTeXversion)
        {
          print "Using non-standard version of pdftex: ",`which pdftex`," ",`pdftex -version`,"\n";
        }
        chdir ($tmp_dir);
        if (!$verbose)
        {
          my $cmdstring = "pdflatex  -interaction=batchmode  $option_target";
          print ("\n===============================================================\n");
          print ("First pass TeXing file: $temp_target\n\n");
          system ( "$cmdstring 1> /dev/null 2> /dev/null ");
          print ("\n===============================================================\n");
          print ("BibTeXing file: $temp_target\n\n");
#          system ( "bibtex      $temp_target 1> $temp_biboutq 2> $temp_biberrq ");
          system ( "bibtex      $temp_target ");
          print ("\n===============================================================\n");
          print ("Second pass TeXing file: $temp_target\n\n");
          system ( "$cmdstring 1> /dev/null 2> /dev/null ");
          print ("\n===============================================================\n");
          print ("Final (third) pass TeXing file: $temp_texq\n\n");
          system ( "$cmdstring 1> $temp_pdfoutq 2> $temp_pdferrq ");
        }
        else
        {
          my $cmdstring = "pdflatex  $option_target";
          print ("\n===============================================================\n");
          print ("First pass TeXing file: $temp_stemq\n\n");
          system ( "$cmdstring");
          print ("\n===============================================================\n");
          print ("BibTeXing file: $temp_stemq\n\n");
          system ( "bibtex      $temp_stem");
          print ("\n===============================================================\n");
          print ("Second pass TeXing file: $temp_stemq\n\n");
          system ( "$cmdstring");
          print ("\n===============================================================\n");
          print ("Final (third) pass TeXing file: $temp_stemq\n\n");
          system ( "$cmdstring");
        }
    }

    if ( !$verbose )
    {
      print "\n-------------------------------------------------------------------\n\n",
      "PdfLaTeX errors (filtered):\n\n";
      open (LOGFILE, $temp_pdflog) || die ("can't open the log file: $!");
      $/ = "\n\n"; # reset EOR... most error messages are terminated by a blank line, so let's use it
      while ( <LOGFILE> ) {
#      print if (/Missing character|Warning:|Error:|Fatal|Undefined|runaway|pdftex Warning/ && !/Citation|Reference|Float|float|Font Warning|weird|multiply\s/);
        print if (/Missing character|Warning:|Error:|Fatal|Undefined|runaway|pdftex Warning/ && !/Float|float|Font Warning|weird|multiply\s/);
        }
    }

    print "\n-------------------------------------------------------------------\n",
          "Output directory: $tmp_dir\n";
    print "-------------------------------------------------------------------\n",    "Output Log files:\n";
    print "$temp_pdflog\n" if -e $temp_pdflog;
    print "$temp_pdfout\n" if -e $temp_pdfout;
    print "$temp_pdferr\n" if -e $temp_pdferr;
    print "$temp_bibout\n" if -e $temp_bibout;
    print "$temp_biberr\n" if -e $temp_biberr;

    print "-------------------------------------------------------------------\n",
          "Output PDF file:";
    print " $temp_pdf\n" if -e $temp_pdf;
    print "-------------------------------------------------------------------\n";
}



#############################################################################
sub clean {
    print "\nRemoving all contents of temporary directory: $tmp_dir\n";
    unlink(bsd_glob("$tmp_dir/*"));
    rmdir($tmp_dir);
}



#############################################################################
sub veryclean {
# could be less crude but good enough for now

    print "\nCleaning everything...\n";
    &clean();

    print "\nDeleting emacs backup files...\n";
    unlink (bsd_glob ("$top_dir/*.*~"));
    unlink (bsd_glob ("$top_dir/*/*.*~"));
    unlink (bsd_glob ("$top_dir/*/*/*.*~"));
    unlink (bsd_glob ("$top_dir/*/*/*/*.*~"));
    unlink (bsd_glob ("$top_dir/*/*/*/*/*.*~"));

    print "\nDeleting nedit backup files...\n";
    unlink (bsd_glob ("$top_dir/*.*.bak"));
    unlink (bsd_glob ("$top_dir/*/*.*.bak"));
    unlink (bsd_glob ("$top_dir/*/*/*.*.bak"));
    unlink (bsd_glob ("$top_dir/*/*/*/*.*.bak"));
    unlink (bsd_glob ("$top_dir/*/*/*/*/*.*.bak"));

    print "\nDeleting all dvi, toc, lof, etc. files...\n";

    unlink (bsd_glob ("$top_dir/*/*.ind"));
    unlink (bsd_glob ("$top_dir/*/*.ilg"));
    unlink (bsd_glob ("$top_dir/*/*.aux"));
    unlink (bsd_glob ("$top_dir/*/*.aidx"));
    unlink (bsd_glob ("$top_dir/*/*.idx"));
    unlink (bsd_glob ("$top_dir/*/*.lof"));
    unlink (bsd_glob ("$top_dir/*/*.lot"));
    unlink (bsd_glob ("$top_dir/*/*.log"));
    unlink (bsd_glob ("$top_dir/*/*.dvi"));
    unlink (bsd_glob ("$top_dir/*/*.toc"));
    unlink (bsd_glob ("$top_dir/*/*.blg"));
    unlink (bsd_glob ("$top_dir/*/*.bbl"));

}


#############################################################################
sub runtime {
#
#   Changes environment variables *OUTSIDE* scope of the script
#   (using ENV is only within scope of script).
#
#   To use this, you need to use eval (see help) to "invoke" the
#   env setting commands which are printed out (see below)
#
    my $shell = $_[0] || "-csh";

#   Add bin directory to PATH (but only if it's not there already)

#    print ('$PATH    = ',"$ENV{PATH} \n");
#    print ('$bin_dir = ',"$bin_dir   \n");

    if ($ENV{PATH} !~ /$bin_dir/){
        print ("\n");

        if ($shell eq "-csh" || $shell eq "-tcsh") {
            print "echo Prepending PATH with: $bin_dir;",
                  "setenv PATH $bin_dir".":$ENV{PATH}\n";
        }
        elsif($shell eq "-sh" || $shell eq "-ksh"){
            print "echo Prepending PATH with: $bin_dir;",
                  'PATH="',"$bin_dir".":$ENV{PATH}",'";',
                  "export PATH\n";
        }
        else{
            die "Unknown shell option: $shell\n\n";
        }
    }
}

#############################################################################
sub test {

    print "\nExecuting tdr test\n-------------------\n\n";

    print '$cur_dir = ',"$cur_dir\n\n";
    print '$top_dir = ',"$top_dir\n";
    print '$bin_dir = ',"$bin_dir\n";
    print '$gen_dir = ',"$gen_dir\n";
    print '$tex_dir = ',"$tex_dir\n";
    print '$fig_path = ',"$fig_path\n";
    print '$bib_path = ',"$bib_path\n\n";
    print '$tmp_dir = ',"$tmp_dir\n\n";

    print '$ENV{TEXINPUTS} = ',"$ENV{TEXINPUTS}\n\n";

}



#############################################################################
sub full_path {
#
# Get full directory path from relative one and context
#
  my $file        = shift;
  my $current_dir = shift || &cwd();
#  print ("full_dir: file= $file, current_dir=$current_dir\n");
  if("$file" eq "")
       {return "";}
  elsif($file=~/^~\/(.*)/)
       {$file="$ENV{HOME}/$1"; }
  elsif($file=~/^~[^\/](.+)\/(.*)/)
       {$file=`dirname ~$1`;
        chomp $file;
        $file="$file/$2";}
  elsif($file=~/^~[^\/](.+)$/)
       {$file=`dirname ~$1`;
        chomp $file;}
  elsif($file=~/^[^\/]/)
       {$file="${current_dir}/$file";}

  return &arrange_path($file);
}

#############################################################################
sub arrange_path {
  my $file=shift;
#  print ("arrange_path: file = $file\n");
  if("$file" eq "") {return "";}

  while($file=~/(.*)\/\.\/(.*)/){$file="$1/$2";}

  if($file=~/(.*)\/\.$/){$file="$1";}

  while($file=~/(.*?)\/\.\.\/(.*)/){
     $file=`dirname $1`;
     chomp $file;
     $file="$file/$2";
  }
  while($file=~/(.*?)\/\.\.$/){
     $file=`dirname $1`;
     chomp $file;
  }
  while($file=~/(.*)\/\/(.*)/) {$file="$1/$2";}
#  print("arrange_path, returning: $file\n");

  return $file;

}
#############################################################################
sub mycat
{
# acts as cat.
# default is append (see second arg in the open command below)

   open(OUTF, ">>", pop(@_)) || die("can't open outputfile: $!");
   FILE: foreach (@_) {

      open(FILE, $_) || ((warn "Can't open file $_\n"), next FILE);

      while (<FILE>) {
         print OUTF;
      }
      close(FILE);
   }
}
#############################################################################
sub myls
{
# acts as a sort of ls
# expects the directory as the first argument and the bsd_glob pattern as the (optional) second argument
  my $dir = shift;
  my $ext = shift;
  if ($ext eq '') {$ext = '*';}
  my @list = bsd_glob($dir.'/'.$ext);
  for (@list) {
#    s|^$dir/||;
    print $_,"\n";
    }
}
